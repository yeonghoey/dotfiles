export DOTFILES_ROOT=$HOME/.dotfiles

# autoload contents of functions
autoload -U $DOTFILES_ROOT/functions/*(:t)

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# load local configuration
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# load oh-my-zsh.
# This should be at last because of order sensitive plugins
source $DOTFILES_ROOT/zsh/oh-my-zsh.sh
